/*
 * Copyright © 2017-2019 WireGuard LLC.
 * Copyright © 2018-2019 Harsh Shandilya <msfjarvis@gmail.com>. All Rights Reserved.
 * SPDX-License-Identifier: Apache-2.0
 */
buildscript {
    apply from: './versions.gradle'
    repositories {
        google()
        jcenter()
        maven { url 'https://plugins.gradle.org/m2/' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:' + versions.gradlePlugin
        classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:' + versions.kotlin
        classpath 'com.diffplug.spotless:spotless-plugin-gradle:' + versions.spotless
    }
}

subprojects {
    apply from: '../versions.gradle'
    repositories {
        google()
        jcenter()
        maven { url 'https://jitpack.io' }
    }
    if (plugins.hasPlugin('kotlin-android')) {
        dependencies {
            implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:' + versions.kotlin
        }
    }
    if (it.name == 'app') {
        apply plugin: 'com.android.application'
    } else {
        apply plugin: 'com.android.library'
    }
    android {
        compileSdkVersion versions.compileSdk
        buildToolsVersion = versions.buildTools
        defaultConfig {
            minSdkVersion versions.minSdk
            targetSdkVersion versions.targetSdk
            versionCode versions.versionCode
            versionName versions.versionName
        }
    }
}

apply from: './spotless.root.gradle'

project(':app') {
    apply from: '../spotless.gradle'
}

project(':native') {
    android {
        buildTypes {
            release {
                externalNativeBuild {
                    cmake {
                        arguments "-DANDROID_PACKAGE_NAME=me.msfjarvis.viscerion"
                    }
                }
            }
            debug {
                externalNativeBuild {
                    cmake {
                        arguments "-DANDROID_PACKAGE_NAME=me.msfjarvis.viscerion.debug"
                    }
                }
            }
        }
    }
}

tasks {
    wrapper {
        distributionType = Wrapper.DistributionType.ALL
    }
}
